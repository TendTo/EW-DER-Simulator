\chapter{Smart Contract: ExamContract}

Per la realizzazione dello \gls{smart-contract}, è stata utilizzata la piattaforma \gls{ewc}.
Il contratto è stato scritto in Solidity, il linguaggio di programmazione orientato agli oggetti, più diffuso per l'implementazione di \gls{smart-contract} su piattaforme blockchain basate su Ethereum. \\

\section{Funzionalità}
Lo \gls{smart-contract} ha come scopo quello di tenere traccia del bilancio di tutti i \gls{prosumer} che fanno riferimento all'\gls{aggregator} che lo ha rilasciato su \gls{ewc}. \\
Ogni \gls{prosumer} sottoscrive un \gls{agreement} con l'\gls{aggregator}, stabilendo i termini della loro collaborazione.
Sono parte di questo accordo la fonte energetica utilizzata, la quantità di energia prodotta e il suo prezzo, e la flessibilità che il \gls{prosumer} è in grado di fornire. \\
In qualsiasi momento il \gls{prosumer} è in grado di avviare un evento di flessibilità.
L'\gls{event-log}, emesso nel momento in cui il metodo predisposto viene invocato, notifica tutti i \gls{der} della richiesta,
ed ognuno di essi reagirà a quest'ultima nei termini del proprio \gls{agreement}. \\
Ogni richiesta è caratterizzata dai seguenti valori:
\begin{itemize}
  \item \textbf{Flessibilità:} variazione che la rete deve produrre rispetto alla \gls{baseline}
  \item \textbf{Inizio:} limite di tempo entro il quale tutti i \gls{der} devono raggiungere il valore di flessibilità richiesto
  \item \textbf{Fine:} fine del periodo di flessibilità. Dal momento indicato, i \gls{der} hanno 15 minuti per tornare alla baseline
\end{itemize}
Non ci possono essere due richieste di flessibilità contemporanee.

\section{Sviluppo dello smart contract}

Lo sviluppo dello smart contract è stato realizzato utilizzando molti framework e librerie open source.

\subsection{Strumenti di sviluppo}

Lo sviluppo dello \gls{smart-contract} si è svolto in locale, utilizzando il framework hardhat \cite{sftw:hardhat},
un ambiente di sviluppo per \gls{smart-contract} dotato di moltissimi componenti
che consentono di svolgere operazioni come il debugging, la compilazione e il testing di \gls{smart-contract}. \\
Una feature aggiuntiva realizzata dal plugin \href{https://www.npmjs.com/package/@typechain/hardhat}{@typechain/hardhat} \cite{sftw:typechain_hardhat} è la creazione automatica di
classi proxy e funzioni che descrivono accuratamente i metodi dello \gls{smart-contract},
che è poi possibile importare ed utilizzare in qualsiasi file typescript, semplificando l'interazione con lo \gls{smart-contract}. \\
\\
hardhat include anche le funzionalità per rilasciare lo \gls{smart-contract} su \gls{ewc}. \\
Fornendo una chiave privata associata ad un account con sufficiente credito e un nodo \gls{rpc-api} a cui collegarsi,
è estremamente facile realizzare un semplice script (\autoref{cod:deployment}) che si occupi di distribuire lo \gls{smart-contract} sulla blockchain scelta.

\inputminted{typescript}{../contracts/scripts/deployAggregatorContract.ts}
\captionof{listing}{Script che si occupa del deployment dello \gls{smart-contract} su \gls{ewc} \label{cod:deployment}}

\subsection{Test}

Il framework utilizzato mette a disposizione anche una serie di librerie per un testing rapido e agevole dello \gls{smart-contract}. \\
Utilizzando un'apposita estensione della libreria chai \cite{sftw:chai}, combinata con una blockchain simulata in locale da hardhat
è possibile effettuare il deploy dello \gls{smart-contract} e verificare che tutti i suoi metodi si comportino come previsto. \\
I test spaziano dal controllo del valore di ritorno del metodo invocato, alla verifica della corretta alterazione dello stato del contratto,
assicurandosi inoltre che venga prodotta la dovuta eccezione in caso di input invalidi. \\
Mettendo insieme tutti i test, è stata raggiunta una \href{https://app.codecov.io/gh/TendTo/EW-DER-Simulator}{notevole copertura} del codice \footnote{https://app.codecov.io/gh/TendTo/EW-DER-Simulator}. \\
\\
Segue qualche esempio di test utilizzato:
\begin{minted}{typescript}
describe("requestFlexibility", function () {

  it("create a new flexibility request", async function () {
 await contract.requestFlexibility(start, end, gridFlexibility);
 const request = await contract.flexibilityRequest();
 expect(request.start.toNumber()).to.equal(start);
 expect(request.end.toNumber()).to.equal(end);
 expect(request.gridFlexibility.toNumber()).to.equal(gridFlexibility);
  });

  it("revert on unauthorized use with 'UnauthorizedAggregatorError(msg.sender)'", async function () {
 await expect(iot1Contract.requestFlexibility(start, end, gridFlexibility))
   .to.be.revertedWithCustomError(contract, ContractError.UnauthorizedAggregatorError)
   .withArgs(iot1Addr);
  });
});
\end{minted}

\section{Strutture dati}

Per la gestione dei dati all'interno della blockchain lo \gls{smart-contract} utilizza un approccio ibrido. \\
Viene salvato in maniera persistente, e quindi occupa spazio di archiviazione nel contratto, solo ciò di cui lo stesso ha bisogno per
svolgere i controlli di validazione previsti. \\
Al contrario, tutto il resto viene reso pubblico ed immutabile attraverso lo strumento degli \gls{event-log},
emessi nel momento in cui avviene l'evento corrispondente. \\
Questo approccio è più economico rispetto allo storage tradizionale, il contratto non è in grado di consultare gli \gls{event-log} emessi in precedenza. \\
\\
Lo storage tradizionale è utilizzato per le strutture dati:
\begin{minted}{solidity}
// Address that deployed the contract. Represents the aggregator
address public immutable aggregator;
// Current aggregated baseline provided by all the DERs 
int256 public energyBalance;
// State of the prosumer, like its balance and reputation
mapping(address => Prosumer) public prosumers;
// Mapping between each prosumer and its agreement
mapping(address => Agreement) public agreements;
// List of prosumers that registered an agreement. 
// Used if they are all to be rewarded
address[] public prosumerList;
// Structure that contains the flexibility request data
FlexibilityRequest public flexibilityRequest;
// Flexibility result for each DER, provided by the aggregator
mapping(address => int256) public flexibilityResults;
\end{minted}

Gli eventi previsti sono:
\begin{minted}[fontsize=\footnotesize]{solidity}
event RegisterAgreement(address indexed prosumer, Agreement agreement);
event ReviseAgreement(address indexed prosumer, Agreement oldAgreement, Agreement newAgreement);
event CancelAgreement(address indexed prosumer, Agreement agreement);
event RequestFlexibility(uint256 indexed start, uint256 indexed stop, int256 gridFlexibility);
event EndRequestFlexibility(uint256 indexed start, uint256 indexed stop, int256 gridFlexibility);
event FlexibilityProvisioningSuccess(uint256 indexed start, address indexed prosumer, int256 flexibility, int256 reward);
event FlexibilityProvisioningError(uint256 indexed start, address indexed prosumer, int256 flexibilityFromAggregator, int256 flexibilityFromProsumer, int256 expectedFlexibility);
event RewardProduction(address indexed prosumer, uint256 indexed timestamp, int256 value, int256 reward);
\end{minted}

\section{Metodi principali}

Di seguito sono elencati i metodi principali e il loro funzionamento, definiti nell'interfaccia IAggregatorContract.

\begin{minted}[fontsize=\footnotesize]{solidity}
function registerAgreement(Agreement calldata _agreement)
  external
  agreementDoesNotExist(msg.sender)
{
  if (_agreement.value == 0) revert ZeroValueError("value");
  // Just to make sure not to add the same address twice. This would be prevented by the modifier
  if (agreements[msg.sender].value == 0) {
      // Register the prosumer in the list of prosumers
      prosumers[msg.sender].idx = prosumerList.length;
      prosumerList.push(msg.sender);
  } else {
      energyBalance -= agreements[msg.sender].value;
  }
  // Set the agreement
  agreements[msg.sender] = _agreement;
  // Change the energy balance
  energyBalance += _agreement.value;

  emit RegisterAgreement(msg.sender, _agreement);
}
\end{minted}
\captionof{listing}{Metodo chiamato da un \gls{prosumer} che intende registrare un \gls{agreement} con l'\gls{aggregator} \label{cod:registerAgreement}}

\begin{minted}[fontsize=\footnotesize]{solidity}
function reviseAgreement(Agreement calldata _agreement) external agreementExists(msg.sender) {
  if (_agreement.value == 0) revert ZeroValueError("value");
  emit ReviseAgreement(msg.sender, agreements[msg.sender], _agreement);

  // Update the energy balance, removing the old value and adding the new one
  energyBalance += _agreement.value - agreements[msg.sender].value;
  // Change the agreement 
  agreements[msg.sender] = _agreement;
}
\end{minted}
\captionof{listing}{Metodo chiamato da un \gls{prosumer} che intende modificare il proprio \gls{agreement} \label{cod:reviseAgreement}}

\begin{minted}[fontsize=\footnotesize]{solidity}
function cancelAgreement() external agreementExists(msg.sender) {
  emit CancelAgreement(msg.sender, agreements[msg.sender]);

  // Remove the prosumer from the list of prosumers
  removeProsumerFromList(msg.sender);
  delete agreements[msg.sender];
  // Remove the agreement
  energyBalance -= agreements[msg.sender].value;
}
\end{minted}
\captionof{listing}{Metodo chiamato da un \gls{prosumer} che intende annullare il proprio \gls{agreement} \label{cod:cancelAgreement}}

\begin{minted}[fontsize=\footnotesize]{solidity}
function requestFlexibility(
  uint256 _start,
  uint256 _end,
  int256 _gridFlexibility
) external isAggregator {
  emit RequestFlexibility(_start, _end, _gridFlexibility);

  // Create a FlexibilityRequest struct, so the parameters can be accessed by future requests
  flexibilityRequest = FlexibilityRequest(_start, _end, _gridFlexibility);
}
\end{minted}
\captionof{listing}{Metodo chiamato dall'\gls{aggregator} quando è necessario richiedere della flessibilità alla rete \label{cod:requestFlexibility}}

\begin{minted}[fontsize=\footnotesize]{solidity}
function endFlexibilityRequest(uint256 _start, FlexibilityResult[] calldata _results)
  external
  isAggregator
{   
  // Check if the request is still valid
  if (flexibilityRequest.start != _start)
      revert FlexibilityRequestNotFoundError(flexibilityRequest.start, _start);

  // Store the results
  for (uint256 i = 0; i < _results.length; i++) {
      flexibilityResults[_results[i].prosumer] = _results[i].flexibility;
  }

  emit EndRequestFlexibility(
      flexibilityRequest.start,
      flexibilityRequest.end,
      flexibilityRequest.gridFlexibility
  );
}
\end{minted}
\captionof{listing}{Metodo chiamato dall'\gls{aggregator} quando la richiesta di flessibilità si e conclusa e si è ritornati alla baseline. \\
  Vengono anche caricati i risultati registrati dall'\gls{aggregator}  \label{cod:endFlexibilityRequest}}

\begin{minted}[fontsize=\footnotesize]{solidity}
function provideFlexibilityFair(uint256 _start, int256 _flexibility)
  external
  agreementExists(msg.sender)
{
  if (_flexibility == 0) revert ZeroValueError("flexibility");
  if (flexibilityRequest.start != _start)
      revert FlexibilityRequestNotFoundError(flexibilityRequest.start, _start);

  // Each prosumer is expected to provide a flexibility proportional
  // to the value it normally provides.
  int256 reward = 0;
  int256 expectedValue = (flexibilityRequest.gridFlexibility * agreements[msg.sender].value) /
      energyBalance;
  int256 result = flexibilityResults[msg.sender];

  if (result == 0) {
      // An error has occurred in the provisioning process.
  } else if (_start != flexibilityRequest.start) {
      // The prosumer didn't request a reward for this flexibility request
  } else if (
      inErrorMargin(_flexibility, result, flexibilityMargin) &&
      inErrorMargin(_flexibility, expectedValue, flexibilityMargin)
  ) {
      // The prosumer will receive the reward
      reward = agreements[msg.sender].flexibilityPrice * result;
      emit FlexibilityProvisioningSuccess(
          flexibilityRequest.start,
          msg.sender,
          result,
          reward
      );
  } else {
      // The flexibility provided does not match the one requested
      // or the one actually provided
      emit FlexibilityProvisioningError(
          flexibilityRequest.start,
          msg.sender,
          result,
          _flexibility,
          expectedValue
      );
  }

  // Assign the reward to the prosumer and delete the result
  prosumers[msg.sender].balance += reward;
  delete flexibilityResults[msg.sender];
}
\end{minted}
\captionof{listing}{Metodo chiamato dai \gls{prosumer}. La flessibilità dichiarata viene confrontata con quella attesa e quella registrata dall'\gls{aggregator}. \\
  Se rientra nel margine di errore, il \gls{prosumer} viene ricompensato  \label{cod:provideFlexibilityFair}}

\begin{minted}[fontsize=\footnotesize]{solidity}
function sendFunds(address payable[] calldata iotAddr) external payable {
  // Value to send to each iot
  uint256 singleValue = msg.value / iotAddr.length;
  for (uint256 i = 0; i < iotAddr.length; i++) {
      iotAddr[i].transfer(singleValue);
  }
}
\end{minted}
\captionof{listing}{Metodo utilizzado dall'\gls{aggregator} nella fase di setup della simulazione per inviare i fondi ai \gls{prosumer} \label{cod:sendFunds}}

\begin{minted}[fontsize=\footnotesize]{solidity}
function resetContract() external isAggregator {
  energyBalance = 0;
  for (uint256 i = 0; i < prosumerList.length; i++) {
      delete agreements[prosumerList[i]];
  }
  delete prosumerList;
  delete flexibilityRequest;
}
\end{minted}
\captionof{listing}{Metodo utilizzado dall'\gls{aggregator} per resettare il contratto fra le simulazioni \label{cod:resetContract}}

\begin{minted}[fontsize=\footnotesize]{solidity}
function selfDestruct() external isAggregator {
  selfdestruct(payable(aggregator));
}
\end{minted}
\captionof{listing}{Metodo utilizzado dall'\gls{aggregator} eliminare il contratto \label{cod:selfDestruct}}

\section{Funzionamento dello smart contract}

Lo \gls{smart-contract} \textit{AggregatorContract} implementa l'interfaccia \textit{IAggregatorContract}
ed è l'unico \gls{smart-contract} che è necessario rilasciare sulla \gls{blockchain} per poter avviare la simulazione. \\
Esso si occupa degli aspetti che riguardano la gestione del bilancio dei singoli prosumer
e stando in ascolto degli \gls{event-log} che produce i \gls{prosumer} possono essere notificati riguardo ad una richiesta di flessibilità da parte dell'\gls{aggregator}. \\
\\
Le prime tre funzioni, \textit{registerAgreement} (\autoref{cod:registerAgreement}), \textit{reviseAgreement} (\autoref{cod:reviseAgreement}) e \textit{cancelAgreement} (\autoref{cod:cancelAgreement}) vengono invocate da un \gls{prosumer} che modificare il proprio \gls{agreement} con il \gls{prosumer}.
Ciò include la creazione dell'\gls{agreement}, l'alterazione dei termini dello stesso o la sua cancellazione. \\
Nello specifico, dopo aver controllato che l'operazione sia consentita, la struttura dati \textit{agreements} viene aggiornata,
e il prosumer viene aggiunto o rimosso dalla lista \textit{prosumerList} e nel mapping \textit{prosumers}.
\\
L'\gls{aggregator} ha la possibilità di invocare il metodo \textit{requestFlexibility} (\autoref{cod:requestFlexibility}) per richiedere della flessibilità alla rete. \\
Questo farà si che venga salvata una struttura dati con i parametri della richiesta quali inizio, fine e flessibilità.
Al contempo, verrà anche emesso un \gls{event-log}.
Tutti i \gls{der} in ascolto saranno notificati della richiesta, e potranno provvedere a soddisfarla. \\
\\
Al termine della richiesta, il \gls{aggregator} invocherà il metodo \textit{endFlexibilityRequest} (\autoref{cod:endFlexibilityRequest}), passando come parametro la lista dei risultati della richiesta che ha registrato.
Dopo aver controllato che l'evento di flessibilità a cui ci riferisce è valido, i valori comunicati saranno salvati in una apposita struttura dati, per poter essere consultati in seguito,
e l'\gls{event-log} \textit{EndRequestFlexibility} sarà emesso. \\
\\
Da questo momento in poi, i \gls{der} hanno la possibilità di invocare il metodo \textit{provideFlexibilityFair} (\autoref{cod:provideFlexibilityFair}) per richiedere la loro ricompensa. \\
Questa verrà aggiunta al loro bilancio a patto che il valore riportato sia coerente con quello comunicato dall'\gls{aggregator} al passaggio precedente. \\
Un evento verrà emesso a seconda del risultato del controllo: \textit{FlexibilityProvisioningSuccess}, se l'operazione è stata un successo, \textit{FlexibilityProvisioningError} altrimenti \\
\\
Sono presenti anche alcune funzioni di utility per poter testare lo \gls{smart-contract} più facilmente.
La funzione \textit{sendFunds} (\autoref{cod:sendFunds}) permette di ripartire la quantità di Volta inviata fra una lista di indirizzi,
e viene utilizzata per inizializzare i \gls{der}.
La funzione \textit{resetContract} (\autoref{cod:resetContract}), invece, azzera le strutture dati.
Infine, la \textit{selfDestruct} (\autoref{cod:selfDestruct}) rimuove lo \gls{smart-contract} dalla \gls{blockchain}.
